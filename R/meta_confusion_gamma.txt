
model
{
  for(i in 1:S){
    y[i,1] ~ dbin(pi[i,1], n[i,1])
    y[i,2] ~ dbin(pi[i,2], n[i,2])
    
    # probability of exposure
    n[i,1] ~ dbin(psi[i], n.tot[i])
    
    psi[i] <- 1 / (1 + exp(-nu[i]))
    
    # conditional prob of event given exposure
    logit(pi[i,1]) <- beta[i] + delta[i]/2
    logit(pi[i,2]) <- beta[i] - delta[i]/2
    
    # meta-regression on the log-odds ratio
    # do we include intercept?
    
    delta[i] ~ dnorm(delta0, D.delta)
    
    nu[i] ~ dnorm(nu0, D.nu)
    
    beta[i] ~ dnorm(beta0, D.beta)
    
  }
  
  beta0 ~ dnorm(a, b)
  delta0 ~ dnorm(c, d)
  nu0 ~ dnorm(e, f)
  
  # Precisions
  D.beta ~ dgamma(3, 2)
  D.nu ~ dgamma(3, 2)
  D.delta ~ dgamma(3, 2)
  
  # gamma prior on precisions
  sigma.beta <- pow(D.beta, -1/2)
  sigma.nu <- pow(D.nu, -1/2)
  sigma.delta <- pow(D.delta, -1/2)

  #draw M new observations for each parameter w/ random effects
  
  for(j in 1:M){
    deltanew[j] ~ dnorm(delta0, D.delta)
    betanew[j] ~ dnorm(beta0, D.beta)
    nunew[j] ~ dnorm(nu0, D.nu)
    psinew[j] <- 1 / (1  + exp(-nunew[j]))
    
    pi1new[j] <- 1 / (1 + exp(-(betanew[j] + deltanew[j] / 2)))
    pi0new[j] <- 1 / (1 + exp(-(betanew[j] - deltanew[j] / 2)))
    
    pi11new[j] <- pi1new[j] * psinew[j]                  # P(event, risk)
    pi10new[j] <- (1 - pi1new[j]) * psinew[j]            # P(no event, risk)
    pi01new[j] <- pi0new[j] * (1 - psinew[j])            # P(event, no risk)
    pi00new[j] <- (1 - pi0new[j]) * (1 - psinew[j])      # P(no event, no risk)
    
    sensnew[j] <- pi11new[j] / (pi11new[j] + pi01new[j])
    specnew[j] <- pi00new[j] / (pi00new[j] + pi10new[j])
    
    PPVnew[j] <- pi11new[j] / (pi11new[j] + pi10new[j])
    NPVnew[j] <- pi00new[j] / (pi00new[j] + pi01new[j])
    
    LRpnew[j] <- sensnew[j] / (1 - specnew[j])
    LRmnew[j] <- (1 - sensnew[j]) / specnew[j]
    
  }
  pi1.h <- 1 / (1 + exp(-(beta0 + delta0 / 2)))
  pi0.h <- 1 / (1 + exp(-(beta0 - delta0 / 2)))
  psi.h <- 1 / (1 + exp(-(nu0)))
  
  pi11.h <- pi1.h * psi.h
  pi10.h <- (1 - pi1.h) * psi.h
  pi01.h <- pi0.h * (1 - psi.h)
  pi00.h <- (1 - pi0.h) * (1 - psi.h)
  
  sens.h <- pi11.h / (pi11.h + pi01.h)
  spec.h <- pi00.h / (pi00.h + pi10.h)
  
  LRp.h <- sens.h / (1 - spec.h)
  LRm.h <- (1 - sens.h) / spec.h
  
}
